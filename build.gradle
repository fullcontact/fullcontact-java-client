buildscript {
    repositories {
        jcenter()
    }
}

group 'com.fullcontact.client'
version '2.3.0'


allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        mavenCentral()
    }
}

def getRepositoryUsername() {
    return hasProperty("ossrhUsername") ? ossrhUsername : ""
}

def getRepositoryPassword() {
    return hasProperty("ossrhPassword") ? ossrhPassword : ""
}

subprojects {

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.18'
        annotationProcessor 'org.projectlombok:lombok:1.18.18'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    javadoc {
        source = sourceSets.main.allJava
        options.memberLevel = JavadocMemberLevel.PUBLIC
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    publishing {
        publications {
            mavenjava(MavenPublication) { publication ->
                from components.java

                artifact javadocJar
                artifact sourcesJar

                pom {
                    name = 'Client libraries for FullContact v3 APIs'
                    description = 'Client libraries for FullContact v3 APIs'
                    url = 'https://github.com/fullcontact/fullcontact-java-client'
                    scm {
                        url = 'https://github.com/fullcontact/fullcontact-java-client'
                        connection = 'scm:git:git://github.com/fullcontact/fullcontact-java-client.git'
                    }
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'FullContact'
                            organization = 'FullContact'
                            organizationUrl = 'https://www.fullcontact.com/'
                        }
                    }
                    organization {
                        name = 'FullContact'
                        url = 'https://www.fullcontact.com/'
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'sonatype'
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username getRepositoryUsername()
                    password getRepositoryPassword()
                }
            }
        }
    }

    signing {
        sign publishing.publications
    }
}

